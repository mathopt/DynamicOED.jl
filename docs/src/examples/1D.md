# Design of Experiments for a simple system

As a pedagogical example, lets start with a simple system.

```math 
\begin{aligned}
\dot{x} &= p_1 x \\ 
y &= x
\end{aligned} 
```

where the state $x$ is dependent on a single, uncertain parameter $p_1$. We assume observe $y$ , e.g. the state directly,  with some fixed measurement rate.

We start by using [ModelingToolkit.jl](https://github.com/SciML/ModelingToolkit.jl) and DynamicOED.jl to model the system.

```@example 1D
using DynamicOED
using ModelingToolkit
using Optimization, OptimizationMOI, Ipopt

@variables t x(t) = 1.0 [description = "State"] 
@variables y(t) [description="Observations", measurement_rate = 0.1]
@parameters p = -2.0 [description= "Uncertain parameter", tunable = true]

D = Differential(t)

@named simple_system = ODESystem(
    [
        D(x) ~ p*x
    ], tspan = (0., 1.), 
    observed = [
        y  ~ x
    ]
)
```

Note that in order to prepare the system for optimal experimental design, we added two important informations:

- The parameter $p_1$ is marked as `tunable`. 
- The observed variable $y$ has a [`measurement_rate`](@ref DynamicOED.VariableRate).

This way, we can limit the set of tunable parameters of the system and derive time grids for the observations independently. To derive the associated system for experimental design, we simply construct an `OEDSystem`. 

```@example 1D
@named oed_system = OEDSystem(simple_system)
```

This step augments the initial system with all necessary information, e.g. the sensitivity equations and the dynamics of the fisher information matrix. The output is simply another `ODESystem`, hence we can use available transformations and simplification routines from ModelingToolkit. 

Next, we associate a optimization criteria to the system and instantiate it over a specific time grid.

```@example 1D
oed_problem = OEDProblem(structural_simplify(oed_system), FisherACriterion())
```

We choose the [`DCriterion`](@ref), which minimizes the determinant of the inverse of the Fisher Information. Given that the optimal solution would be to measure all occurances, we also need to add constraints to the measurement function.

```@example 1D
optimization_variables = states(oed_problem)
```

!!! note 
    The `optimization_states` contain several groups of variables, namely `measurements`, `controls`, `initial_conditions`, and `regularization`. `measurements` represent the decision to observe at a specific timepoint at the grid. We currently work with the naming convention `w_i` for the i-th observed equation.

Now we have access to all optimization variables as a `ComponentArray`. We are interested in choosing at most 3 measurements, so we add a `ConstraintSystem` from ModelingToolkit.

```@example 1D
constraint_equations = [
    sum(optimization_variables.measurements.w₁) ≲ 3,
]

@named constraint_system = ConstraintsSystem(
    constraint_equations, optimization_variables, []
)
nothing # hide
```

Finally, we are now able to convert our [`OEDProblem`](@ref) into an `OptimizationProblem` and `solve` it.

```@example 1D
optimization_problem = OptimizationProblem(
    oed_problem, AutoForwardDiff(), constraints = constraint_system,
    integer_constraints = false
)

optimal_design = solve(optimization_problem, Ipopt.Optimizer())
```

We see that we have indeed recovered a bang-bang solution to our problem, even though we only solve a relaxed problem. The interested reader can find a more in depth explation [here](https://mathopt.de/PUBLICATIONS/Sager2005.pdf). 
